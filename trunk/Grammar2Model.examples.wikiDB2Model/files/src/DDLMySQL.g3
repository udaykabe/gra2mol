grammar DDLMySQL;

options {
	backtrack=true;
}  

data_definition 
	: 	(data_definition_statements | data_manipulation_statements | administration_statements | utility_statements)*
	;

administration_statements
	:	set_definition
	;

set_definition
	:	'SET' variable_assignment (',' variable_assignment)* ';'
	; 

variable_assignment
	:	ID '=' DQVALUE
    | 	('GLOBAL' | 'SESSION') ID '=' DQVALUE
    | 	'@' '@' ID '=' (DQVALUE | '@' '@' ID)
    ;
    
utility_statements
	: 	use_statement
	;

use_statement
	: 	'USE' ID ';'
	;
	
data_definition_statements
	: 	create_database
	| 	create_table
	|	drop_table
	;	

create_database
	:	'CREATE' ('DATABASE' | 'SCHEMA') ('IF' 'NOT' 'EXISTS')? ID (create_specification)? ';'
	;

create_specification
	:	('DEFAULT')? 'CHARACTER' 'SET' ('=')? ID
  	|	('DEFAULT')? 'COLLATE' ('=')? ID
  	;

create_table 
	:	'CREATE' ('TEMPORARY')? 'TABLE' ('IF' 'NOT' 'EXISTS')? OQUOTE ID OQUOTE
		'(' 
			create_definition (',' create_definition)* 
		')' (table_options)* ';'
	;

create_definition
	: 	col_name column_definition
	| 	('UNIQUE' | 'PRIMARY' | 'FULLTEXT')? ('INDEX' | 'KEY') (OQUOTE ID OQUOTE)? (index_type)? 
		('(' index_col_name (',' index_col_name)* ')')? (index_option)? 
	;
	
column_definition
	: 	data_type ('NOT')? ('NULL')? ('DEFAULT' (ID | QVALUE | 'NULL'))? ('ON' ('UPDATE' | 'DELETE') ID)?
      	('AUTO_INCREMENT')? (('UNIQUE' | 'PRIMARY') 'KEY')?
      	('COMMENT' ID)?
      	('COLUMN_FORMAT' ('FIXED' | 'DYNAMIC' | 'DEFAULT'))?
      	('STORAGE' ('DISK'|'DEFAULT'))? // Falta 'MEMORY' dentro del OR
      	(reference_definition)?
	;
	
index_col_name
	:	col_name ('(' NUMBER ')')? ('ASC' | 'DESC')?
	;

index_type
	:	'USING' ('BTREE' | 'HASH' | 'RTREE')
	;
	
index_option
	:	'KEY_BLOCK_SIZE' ('=')? NUMBER
  	| 	index_type
  	| 	'WITH' 'PARSER' OQUOTE ID OQUOTE
	;

data_type
	:	'BIT' ('(' NUMBER ')')?
  	| 	'TINYINT' ('(' NUMBER ')')? ('UNSIGNED')? ('ZEROFILL')?
  	| 	'SMALLINT' ('(' NUMBER ')')? ('UNSIGNED')? ('ZEROFILL')?
  	| 	'MEDIUMINT' ('(' NUMBER ')')? ('UNSIGNED')? ('ZEROFILL')?
  	| 	'INT' ('(' NUMBER ')')? ('UNSIGNED')? ('ZEROFILL')?
  	| 	'INTEGER' ('(' NUMBER ')')? ('UNSIGNED')? ('ZEROFILL')?
  	| 	'BIGINT' ('(' NUMBER ')')? ('UNSIGNED')? ('ZEROFILL')?
  	| 	'REAL' ('(' NUMBER ')')? ('UNSIGNED')? ('ZEROFILL')?
 	| 	'DOUBLE' ('(' NUMBER ')')? ('UNSIGNED')? ('ZEROFILL')?
 	| 	'FLOAT' ('(' NUMBER ')')? ('UNSIGNED')? ('ZEROFILL')?
  	| 	'DECIMAL' ('(' NUMBER (',' NUMBER)? ')')? ('UNSIGNED')? ('ZEROFILL')?
  	| 	'NUMERIC' ('(' NUMBER (',' NUMBER)?')')? ('UNSIGNED')? ('ZEROFILL')?
  	| 	'DATE'
  	| 	'TIME'
  	| 	'TIMESTAMP'
  	|	'DATETIME'
  	| 	'YEAR'
  	| 	'CHAR' ('(' NUMBER ')')? ('CHARACTER' 'SET' ID)? ('COLLATE' ID)?
  	| 	'VARCHAR' '(' NUMBER ')' ('CHARACTER' 'SET' ID)? ('COLLATE' ID)?
  	| 	'BINARYTYPE' '(' NUMBER ')'
  	| 	'VARBINARY' '(' NUMBER ')'
  	| 	'TINYBLOB'
  	| 	'BLOB'
  	| 	'MEDIUMBLOB'
  	| 	'LONGBLOB'
  	| 	'TINYTEXT' ('BYNARY')? ('CHARACTER' 'SET' ID)? ('COLLATE' ID)?
   	| 	'TEXTVAR' ('BYNARY')? ('CHARACTER' 'SET' ID)? ('COLLATE' ID)?
  	| 	'MEDIUMTEXT' ('BYNARY')? ('CHARACTER' 'SET' ID)? ('COLLATE' ID)?
  	| 	'LONGTEXT' ('BYNARY')? ('CHARACTER' 'SET' ID)? ('COLLATE' ID)?
  	| 	'ENUM' '(' QVALUE (',' QVALUE)* ')' ('CHARACTER' 'SET' ID)? ('COLLATE' ID)?
  	| 	'SET' '(' QVALUE (',' QVALUE)* ')'  ('CHARACTER' 'SET' ID)? ('COLLATE' ID)?
;

table_options
	:	'ENGINE' ('=')? ID
  	| 	'AUTO_INCREMENT' ('=')? NUMBER
  	| 	'AVG_ROW_LENGTH' ('=')? NUMBER
  	| 	('DEFAULT')? 'CHARACTER' 'SET' ('=')? ID
  	| 	('DEFAULT')? 'CHARSET' ('=')? ID
  	| 	'CHECKSUM' ('=')? NUMBER
  	| 	('DEFAULT')? 'COLLATE' ('=')? ID
  	|	'COMMENT' ('=')? DOUBLEQUOTE DQVALUE DOUBLEQUOTE
  	| 	'CONNECTION' ('=')? DOUBLEQUOTE DQVALUE DOUBLEQUOTE
  	//| 	'DATA DIRECTORY' ('=')? DOUBLEQUOTE DQVALUE DOUBLEQUOTE
  	| 	'DELAY_KEY_WRITE' ('=')?  NUMBER
  	|	'INDEX DIRECTORY' ('=')? DOUBLEQUOTE DQVALUE DOUBLEQUOTE
  	| 	'INSERT_METHOD' ('=')? ('NO' | 'FIRST' | 'LAST')
  	| 	'KEY_BLOCK_SIZE' ('=')? NUMBER
  	| 	'MAX_ROWS' ('=')? NUMBER
  	| 	'MIN_ROWS' ('=')? NUMBER
  	| 	'PACK_KEYS' ('=')? (NUMBER | 'DEFAULT')
  	//| 	'PASSWORD' ('=')? DOUBLEQUOTE DQVALUE DOUBLEQUOTE
  	| 	'ROW_FORMAT' ('=')? ('DEFAULT'|'DYNAMIC'|'FIXED'|'COMPRESSED'|'REDUNDANT'|'COMPACT')
  	| 	'TABLESPACE' ID ('STORAGE' ('DISK'|'DEFAULT'))? // Falta 'MEMORY' dentro del OR
  	| 	'UNION' ('=')? ID (',' ID)*
	;


reference_definition
	:	'REFERENCES' OQUOTE ID OQUOTE '(' index_col_name (',' index_col_name)* ')'
      	('MATCH' 'FULL' | 'MATCH' 'PARTIAL' | 'MATCH' 'SIMPLE')?
      	('ON' 'DELETE' reference_option)?
      	('ON' 'UPDATE' reference_option)?
    ;
reference_option
	:	'RESTRICT' 
	| 	'CASCADE' 
	| 	'SET' 'NULL'
	| 	'NO' 'ACTION'
	;

drop_table
	: 	'DROP' ('TEMPORARY')? 'TABLE' ('IF' 'EXISTS')? OQUOTE ID OQUOTE (',' OQUOTE ID OQUOTE)*
    	('RESTRICT' | 'CASCADE')? ';'
	;
	
data_manipulation_statements
	:	insert_statement
	;

insert_statement
	: 	'INSERT' ('LOW_PRIORITY' | 'DELAYED' | 'HIGH_PRIORITY')? ('IGNORE')?
    	('INTO')? OQUOTE ID OQUOTE ('(' col_name (',' col_name)* ')')?
    	('VALUES') insert_value (',' insert_value)* ';' // falta 'VALUE' en el primer parentesis
    ;

col_name
	:  	OQUOTE ID OQUOTE
	;

insert_value
	:	'(' insert_data (',' insert_data)* ')'
	;

insert_data
	:	QVALUE*
	| 	NUMBER
	|	DECNUMBER
	| 	HEXNUMBER
	| 	'DEFAULT'
	| 	'NULL'
	;
	
DOT			: '\u002E';
SLASH		: '\u005C';
OQUOTE		: '\u0060';
QUOTE		: '\u0027';
QVALUE 		: QUOTE (options {greedy=false;} : .)* QUOTE;
DOUBLEQUOTE	: '\u0022';
DQVALUE		: DOUBLEQUOTE (options {greedy=false;} : .)* DOUBLEQUOTE;
NUMBER 		: ('+' | '-')? ('0'..'9')+;
DECNUMBER	: ('+' | '-')?  NUMBER DOT NUMBER+;
HEXNUMBER	: ('+' | '-')?  '0' ('x' | 'X') ('0'..'9' | 'a'..'f' | 'A'..'F')+;
ID 			: ('a'..'z' | 'A'..'Z') ( 'a' .. 'z' | 'A' .. 'Z' | '0' .. '9' | '_' | '$' | '#' | SLASH | '-')*;
IDNUMBER 	: ('a'..'z' | 'A'..'Z' | '0'..'9') ( 'a' .. 'z' | 'A' .. 'Z' | '0' .. '9' | '_' | '$' | '#' )*;

WS 			: (' ' | '\t' | '\n' | '\r')+ {$channel=HIDDEN;};
ML_COMMENT:
	'/*' (options {greedy=false;} : .)* '*/' (';')? { $channel=HIDDEN;}
	;
	
OL_COMMENT: '--' ( options {greedy=false;} : . )* '\n'+ { $channel=HIDDEN;}
	;
